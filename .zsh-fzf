# vim: fdm=marker

#                 ██              ████          ████
#                ░██             ░██░          ░██░
#  ██████  ██████░██            ██████ ██████ ██████
# ░░░░██  ██░░░░ ░██████  █████░░░██░ ░░░░██ ░░░██░
#    ██  ░░█████ ░██░░░██░░░░░   ░██     ██    ░██
#   ██    ░░░░░██░██  ░██        ░██    ██     ░██
#  ██████ ██████ ░██  ░██        ░██   ██████  ░██
# ░░░░░░ ░░░░░░  ░░   ░░         ░░   ░░░░░░   ░░
#
#  ~/.zshrc
#  ~/.zsh-local
#
#
#  TODO: Ideas on how an ideal fzf-based shell should work for me
#       - DONE: fl: fuzzy locate any file/folder. File => open with peek, folder => cd
#       - TODO: fh: same as fuzzy locate, but in the current directory
#       - No app should require an argument, just like fl works

ZSH_CACHE_DIR=~/.cache

# Common fzf preview command
FZF_PREVIEW_CMD='scope.sh {q} {1}'
fzf-history-widget-accept() {
  fzf-history-widget
  zle accept-line
}
zle     -N     fzf-history-widget-accept
bindkey '^X^R' fzf-history-widget-accept
# bindkey '^R' fzf-history-widget

if [[ -x "$(command -v rg)" ]]; then 
    export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow -g "!{.git,node_modules}/*" -g "!*.o" 2> /dev/null'
elif [[ -x "$(command -v fd)" ]]; then 
    export FZF_DEFAULT_COMMAND='fd --hidden --follow --exclude .git'
else
    export FZF_DEFAULT_COMMAND='find . -path ./.dropbox-dist -prune -o -printf "%P\\n"'
fi

# export FZF_CTRL_T_COMMAND='find . -path ./.dropbox-dist -prune -o -printf "%P\\n"'
export FZF_CTRL_T_COMMAND='fd -t f'
export FZF_ALT_C_COMMAND='fd -t d'
export FZF_ALT_C_OPTS="--preview-window wrap --preview 'scope.sh {q} {1}'"
export FZF_CTRL_T_OPTS="--preview-window wrap --preview 'scope.sh {q} {1}'"
export FZF_DEFAULT_OPTS="--preview-window wrap --bind 'ctrl-v:toggle,ctrl-s:toggle+down,ctrl-n:down,ctrl-p:up,ctrl-x:execute@nohup xdg-open {} >/dev/null 2>&1 & disown@,ctrl-e:execute@nohup litepub {} >/dev/null 2>&1 & disown@','ctrl-r:execute@frifle {}@','alt-j:half-page-down','alt-k:half-page-up','ctrl-f:jump' --color='bg+:#5E81AC,prompt:#EBCB8B' --reverse"

if command -v fd &>/dev/null; then
    # Use fd (https://github.com/sharkdp/fd) instead of the default find
    # command for listing path candidates.
    # - The first argument to the function ($1) is the base path to start traversal
    # - See the source code (completion.{bash,zsh}) for the details.
    _fzf_compgen_path() {
      fd --hidden --follow --exclude ".git" . "$1"
    }

    # Use fd to generate the list for directory completion
    _fzf_compgen_dir() {
      fd --type d --hidden --follow --exclude ".git" . "$1"
    }
fi

# fl - fzf-locate: {{{
fzf_locate()
{
    # Fuzzy locate any file/folder in home directory
    # Pipes fd output to fuzscope for efficient filtering and preview
    local OUT FILE

    OUT=$(fd -L --base-directory="$HOME" "$@" . | fuzscope --base-directory "$HOME")

    if [[ -n "$OUT" ]]; then
        # Get last selected line (handles multiple selections from peek)
        FILE="${OUT##*$'\n'}"
        [ -d "$FILE" ] && cd "$FILE" || echo "$OUT" | peek
    fi
}

alias fl="fzf_locate"
zle -N fzf_locate
bindkey '^F' fzf_locate
# bindkey -M viins '^F' fzf_locate
# bindkey -M vicmd '^F' fzf_locate

# }}}

#fj - fuzzy cd (jump) from anywhere (auto){{{
#ex: cf word1 word2 ... (even part of a file name)
fj() {
  local file

  # file="$(locate -Ai -0 $@ | grep -z -vE '~$' | grep -z -vE '.local/share/nvim|gutentags_cache_dir|.cache/*|.cpan' | fzf --read0 -0 -1)"
  file=$(fd --base-directory=$HOME -L "$@" . | fuzscope --base-directory "$HOME")

  if [[ -n $file ]]
  then
     if [[ -d $file ]]
     then
        cd -- $file
     else
        cd -- ${file:h}
     fi
  fi
}

#}}}

# vg: persistent fuzzy grep-edit via rg/ag/grep {{{
vg() {
    if [ ! "$#" -gt 0 ]; then echo "Need a string to search for!"; return 1; fi
    if command -v rg &>/dev/null; then
        rg -S --no-heading --no-messages --line-number "$@" | fzf -e -d ':' \
            --preview 'rg --pretty --with-filename --context 3 . {1} | sed -n "$((({2}-3>0?{2}-3:1)),({2}+3))p"' \
            --bind "enter:execute: $EDITOR {1} +{2}"
    elif command -v ag &>/dev/null; then
        ag -S --nobreak --noheading "$@" | fzf -e -d ':' --bind "enter:execute: $EDITOR {1} +{2}"
    else
        grep -irEnH "$@" | fzf -e -d ':' --bind "enter:execute: $EDITOR {1} +{2}"
    fi
}

#}}}

# dj - jump to selected directory {{{
dj() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

#}}}

# dja - including hidden directories {{{
dja() {
  local dir
  dir=$(find ${1:-.} -type d 2> /dev/null | fzf +m) && cd "$dir"
}
#}}}

# dp - cd to selected parent directory {{{
dp() {
  local declare dirs=()
  get_parent_dirs() {
    if [[ -d "${1}" ]]; then dirs+=("$1"); else return; fi
    if [[ "${1}" == '/' ]]; then
      for _dir in "${dirs[@]}"; do echo $_dir; done
    else
      get_parent_dirs $(dirname "$1")
    fi
  }
  local DIR=$(get_parent_dirs $(realpath "${1:-$PWD}") | fzf-tmux )
  cd "$DIR"
}
#}}}

# fs: fuzzy subdir - cd into the directory of the selected file {{{
fs() {
   local file
   file=$(fd | fzf +m -q "$1")

  if [[ -n $file ]]
  then
     if [[ -d $file ]]
     then
        cd -- $file
     else
        cd -- ${file:h}
     fi
  fi
}
#}}}

# fe: fuzzy edit- edit selected file
fe() {
   local file
   file=$(fd | fzf +m -q "$1")
   [ -f "$file" ] || return

   "$EDITOR" "$file"
}
#}}}

# GIT {{{
#
# fshow - git commit browser

alias glNoGraph='git log --color=always --format="%C(auto)%h%d %s %C(cyan)%C(bold)%cr% C(auto)%an" "$@"'
_gitLogLineToHash="echo {} | grep -o '[a-f0-9]\{7\}' | head -1"
_viewGitLogLine="$_gitLogLineToHash | xargs -I % sh -c 'git show --color=always % | less '"

fshow() {
  git log --graph --color=always \
      --format="%C(auto)%h%d %s %C(cyan)%C(bold)%cr" "$@" |
  fzf --ansi --no-sort --reverse --tiebreak=index --bind=ctrl-s:toggle-sort \
        --header "enter to view, ctrl-y to copy hash" \
        --bind "ctrl-y:execute:$_gitLogLineToHash | xclip -i -selection clipboard" \
      --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 |
                xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
FZF-EOF"
}

# fshow_preview - git commit browser with previews
fshow_preview() {
    glNoGraph |
        fzf --no-sort --reverse --tiebreak=index --no-multi \
            --ansi --preview="$_viewGitLogLine" \
                --header "enter to view, ctrl-y to copy hash" \
                --bind "enter:execute:$_viewGitLogLine   | less -R" \
                --bind "ctrl-y:execute:$_gitLogLineToHash | xclip -i -selection clipboard"
}

#}}}

# fjrnl: {{{
fjrnl() {
    local title=$(jrnl --short | fzf --tac --no-sort)
    [[ -n "$title" ]] && jrnl -on "${title:0:16}" $1
}
#}}}

# Autojump: {{{
j() {
    if ! command -v autojump &>/dev/null; then
        fj
        return
    fi

    if [[ "$#" -ne 0 ]]; then
        cd $(autojump "$@")
        return
    fi
    cd "$(autojump -s | awk '/_____/{exit} {$1=""; print substr($0,2)}' |  fzf --height 40% --tac)"
}

k() {
    if ! command -v zoxide &>/dev/null; then
        fj
        return
    fi

    if [[ "$#" -ne 0 ]]; then
        cd $(__zoxide_z "$@")
        return
    fi
    cd "$(zoxide query --list --score | awk '{$1=""; print substr($0,2)}' |  fzf --height 40% --tac)"
}
#}}}

# fzf-locate-ALT+I:{{{
# ALT-I - Paste the selected entry from locate output into the command line
fzf-locate-widget() {
  local selected
  # Use fd instead of locate for better performance
  if selected=$(fd . / --hidden --follow --type f 2>/dev/null | fzf -q "$LBUFFER"); then
    LBUFFER=$selected
  fi
  zle redisplay
}
zle -N fzf-locate-widget
# bindkey '\ei' fzf-locate-widget
bindkey '^o' fzf-locate-widget

#}}}

# p: {{{
p () {
    open=xdg-open   # this will open pdf file withthe default PDF viewer on KDE, xfce, LXDE and perhaps on other desktops.

    ag -U -g ".pdf$" \
    | fast-p \
    | fzf --read0 --reverse -e -d $'\t'  \
        --preview-window down:80% --preview '
            v=$(echo {q} | tr " " "|"); 
            echo -e {1}"\n"{2} | grep -E "^|$v" -i --color=always;
        ' \
    | cut -z -f 1 -d $'\t' | tr -d '\n' | xargs -r --null $open > /dev/null 2> /dev/null
}

#}}}

# FSH / Fuzzy SSH: {{{
fsh() {
  ssh -Y "$(grep '^Host\b' $HOME/.ssh/config* | awk '{print $2}' | fzf -q "$1" -1)"
}

fst() {
  grep '^Host\b' $HOME/.ssh/config* | awk '{print $2}' | fzf -m -q "$1" -1 | while read host ; do
  ssh -O stop "$host"
done
}

#}}}

# fsend:{{{
fsend() {
    # TODO: Allow depth=1 for home dir
    # TODO: Fix for files with spaces
    local BAK="$FZF_DEFAULT_COMMAND"
    # export FZF_DEFAULT_COMMAND='find . -path ./.dropbox-dist -prune -o -printf "%P\\n"'
    export FZF_DEFAULT_COMMAND='fd .'

    files=$(fzf -m --reverse -q "$1")
    [ -z "$files" ] && return

    host=$(grep '^Host\b' $HOME/.ssh/config* | awk '{print $2}' | fzf)
    [ -z "$host" ] && return

    host=$host":sent"

    while IFS='\n' read -r file; do
        scp -rC "$file" "$host"
    done<<<"$files"

    export FZF_DEFAULT_COMMAND="$BAK"
}

# }}}

# cd-files: {{{
cd() {
    if [[ "$#" != 0 ]]; then
        builtin cd "$@"
        return
    fi

    while true; do
        # Build listing once before fzf, not inside the loop
        local lsd=$(echo ".." && ls -pL --group-directories-first --color=never)
        local sel="$(printf '%s\n' "$lsd" | fzf --reverse --print-query --expect=ctrl-e --bind="ctrl-y:execute@echo ${PWD}/{} | xclip -i -selection clipboard@" --preview 'scope.sh {q} {}')"

        [[ -z "$sel" ]] && return 0

        # Parse fzf output using parameter expansion instead of sed for efficiency
        local lines=("${(@f)sel}")
        local query="${lines[1]}"
        local key="${lines[2]}"
        local fname="${lines[3]}"

        if [[ -f "$fname" ]] && command -v filer &>/dev/null; then
            printf '%s\n' "$query" "$fname" | filer
        elif [[ -f "$query" ]] && command -v filer &>/dev/null; then
            printf '%s\n' "$query" "$fname" | filer
        elif [[ -d "$fname" ]]; then
            builtin cd "$fname" &>/dev/null
        elif [[ -d "$query" ]]; then
            builtin cd "$query" &>/dev/null
        fi
    done
}
# }}}

# fkill: {{{
fkill() {
    pid=$(ps -ef | \
        sed 1d | \
        fzf -m --preview="ps -p {2}" --preview-window=down:3 | \
        awk '{print $2}')

    if [ "x$pid" != "x" ]
    then
        kill -"${1:-9}" "$pid"
    fi
}
# }}}

## fuzzy locate config: {{{
fzf_locate_config() {
    local QUERY
    [ $# -gt 0 ] && QUERY="--query=\"$*\"" || QUERY=""

    # fd . ~ | fuzscope "$QUERY" | filer
    # The above one liner resolves the 'builtin cd' command inside filer, which doesn't work. Here's another option

    # OUT=$(locate home media | fuzscope "$QUERY" )
    local OUT=$(fd . -L --base-directory=$HOME/.config | fuzscope --base-directory $HOME/.config $QUERY)
    if [[ -n "$OUT" ]]; then
        FILE="${OUT##*$'\n'}"
        [ -d "$FILE" ] && cd "$FILE" || echo "$OUT" | peek
    fi
}
alias flc="fzf_locate_config"

# }}}

# function fuzscope()
# {
#     fzf "$@"\
#         -0 -1 -e -d "\t" \
#         --preview="scope.sh {q} {1} | bat" \
#         --preview-window up:75% \
#         --expect=ctrl-e,ctrl-b \
#         --bind 'ctrl-space:execute@nohup xdg-open {1} >/dev/null 2>&1 & disown@,ctrl-y:execute@echo {1} | xclip -i -selection clipboard@,ctrl-x:execute@nohup xdg-open {1} >/dev/null 2>&1 & disown@'
# }


fzf_copy() {
    # [ $# -gt 0 ] && QUERY="--query=\"$@\"" || QUERY=""
    # OUT=$(fd . ~ | fuzscope -m "$QUERY" | tail -n+2)

    local OUT=$(fd "$@" . ~ | fuzscope -m)

    while IFS= read -r file; do
        cp -r "$file" .
    done <<< "$OUT"
}

alias fcp="fzf_copy"
zle -N fzf_copy
